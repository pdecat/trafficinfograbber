buildscript {
    repositories {
        google()
    }
    dependencies {
        classpath 'me.moallemi.gradle:advanced-build-version:2.0.2'
    }
}

plugins {
    id 'com.android.application'
    id "com.github.hierynomus.license" version "0.16.1"
    id 'com.github.triplet.play' version '3.8.4'
}

apply plugin: 'me.moallemi.advanced-build-version'

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment:2.7.1'
    implementation 'androidx.navigation:navigation-ui:2.7.1'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }
}

advancedVersioning {
    nameOptions {
        versionMajor 1
        versionMinor 1
        versionPatch 1
    }
    codeOptions {
        versionCodeType 'AUTO_INCREMENT_ONE_STEP'
        dependsOnTasks 'release', 'publish'
    }
    outputOptions {
        renameOutput true
    }
}

android {
    namespace = "org.decat.tig"

    compileSdk 34

    defaultConfig {
        applicationId "org.decat.tig"
        minSdk 14
        targetSdk 34
        versionCode advancedVersioning.versionCode
        versionName advancedVersioning.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(String.valueOf(System.getenv("KEYSTORE_PATH")))
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEYSTORE_ALIAS")
            keyPassword System.getenv("KEYSTORE_ALIAS_PASSWORD")
        }
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        debug {
            applicationIdSuffix = ".debug"
            signingConfig signingConfigs.release
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    lint {
        abortOnError false
    }

}

advancedVersioning.renameOutputApk()

license {
    header = file('LICENSE.txt')
    header project.file('src/main/res/raw/license_short.txt')
    excludes([
            "**/recent_changes.txt",
    ])
    strictCheck true
}
